// Filename: build.gradle
// Author: Olivier Sirol <czo@free.fr>
// License: GPL-2.0 (http://www.gnu.org/copyleft)
// File Created: avril 2012
// Last Modified: lundi 18 avril 2022, 15:06
// Edit Time: 1:40:25
// Description: app build.gradle

apply plugin: 'com.android.application'

println("")
int minutesSinceEpoch = System.currentTimeMillis() / 1000 / 60
def keystorePropertiesFile = rootProject.file("local.properties")
def keystoreProperties = new Properties()
def canSign = false
if (keystorePropertiesFile.exists()) {
    canSign = true
    println("-> release will be signed")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    println("-> versionCode: " + minutesSinceEpoch)
    compileSdkVersion 32
    defaultConfig {
        applicationId "org.czo.badumtsss"
        minSdkVersion 9
        targetSdkVersion 32
        versionCode minutesSinceEpoch
        versionName "1.42"
    }

    if (canSign) {
        signingConfigs {
            release {
                storeFile file(keystoreProperties['key.store'])
                storePassword keystoreProperties['key.store.password']
                keyAlias keystoreProperties['key.alias']
                keyPassword keystoreProperties['key.alias.password']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
             if (canSign) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
